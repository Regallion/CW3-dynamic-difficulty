#Harasser portal script. Creates a harasser when timer elapses. 


once 
	<-HarasserManager 0 eq if
		self 0 destroy 
		return
	endif
	ClearTraceLog
	HideTraceLog
	<-BloomTimer 30 mul 45 mul 0 round SetTimer0
	@awake
endonce 

GetTimer1 eq0 if
	16 CurrentCoords CellToPixel -1 0.5 dup 0.02 90 CreateEffectClipped 
	90 SetTimer1
	GetTimer0 30.00 div 0 Round SetText
	"Retro3" PlaySound
endif
"Will summon a harasser in " GetTimer0 30.00 div 0 Round " seconds." concat concat SetPopupText
 GetTimer0 eq0  if
	"CRPLCORE" CurrentCoords CreateUnit ->Harasser
	"Retro10" PlaySound
	<-Harasser "Harasser.crpl" AddScriptToUnit
	<-Harasser "Harasser.crpl" "HarasserManager" <-HarasserManager SetScriptVar
	<-Harasser "Harasser.crpl" "Chunk" <-Chunk SetScriptVar
	<-Harasser "Harasser.crpl" "Emissions" <-Emissions SetScriptVar
	<-Harasser "Harasser.crpl" "Interval" <-Interval SetScriptVar
	<-Harasser "Harasser.crpl" "DelayTimer" <-DelayTimer SetScriptVar
	<-HarasserManager "HarasserManager.crpl" "Harassers" GetScriptVar ->Harassers
	<-Harassers <-Harasser AppendToList
	<-HarasserManager "HarasserManager.crpl" "Harassers" <-Harassers SetScriptVar 
	Self 0 Destroy
 endif
 


:awake
	Self CONST_COUNTSFORVICTORY 0 SetUnitAttribute 
	Self CONST_NULLIFIERDAMAGES 1 SetUnitAttribute 
	Self CONST_SUPPORTSDIGITALIS 0 SetUnitAttribute
	Self CONST_CREATEPZ 0 SetUnitAttribute
	Self "main" "" SetImage # TODO
	Self "main" 0 0 0 0 SetImageColor
	Self CONST_TAKEMAPSPACE 1 SetUnitAttribute
	
:destroyed
<-Chunk "Chunk.crpl" "Harassed" 0 SetScriptVar
<-HarasserManager "HarasserManager.crpl" "Harassers" GetScriptVar ->Harassers
<-Harassers GetListCount 0 do
		<-Harassers I GetListElement Self eq if
			<-Harassers I RemoveListElement
			break
		endif
loop 
<-HarasserManager "HarasserManager.crpl" "Harassers" <-Harassers GetScriptVar 